{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Eval"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (eval exp env)\n",
    "  (cond ((self-evaluating? exp) \n",
    "         exp)\n",
    "        ((variable? exp) \n",
    "         (lookup-variable-value exp env))\n",
    "        ((quoted? exp) \n",
    "         (text-of-quotation exp))\n",
    "        ((assignment? exp) \n",
    "         (eval-assignment exp env))\n",
    "        ((definition? exp) \n",
    "         (eval-definition exp env))\n",
    "        ((if? exp) \n",
    "         (eval-if exp env))\n",
    "        ((lambda? exp)\n",
    "         (make-procedure \n",
    "          (lambda-parameters exp)\n",
    "          (lambda-body exp)\n",
    "          env))\n",
    "        ((begin? exp)\n",
    "         (eval-sequence \n",
    "          (begin-actions exp) \n",
    "          env))\n",
    "        ((cond? exp) \n",
    "         (eval (cond->if exp) env))\n",
    "        ((application? exp)\n",
    "         (apply (eval (operator exp) env)\n",
    "                (list-of-values \n",
    "                 (operands exp) \n",
    "                 env)))\n",
    "        (else\n",
    "         (error \"Unknown expression \n",
    "                 type: EVAL\" exp))))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Apply"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (apply procedure arguments)\n",
    "  (cond ((primitive-procedure? procedure)\n",
    "         (apply-primitive-procedure \n",
    "          procedure \n",
    "          arguments))\n",
    "        ((compound-procedure? procedure)\n",
    "         (eval-sequence\n",
    "           (procedure-body procedure)\n",
    "           (extend-environment\n",
    "             (procedure-parameters \n",
    "              procedure)\n",
    "             arguments\n",
    "             (procedure-environment \n",
    "              procedure))))\n",
    "        (else\n",
    "         (error \"Unknown procedure \n",
    "                 type: APPLY\" \n",
    "                procedure))))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Procedure arguments"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Exercise 4.1: \n",
    "\n",
    "Notice that we cannot tell whether the metacircular evaluator evaluates operands from left to right or from right to left. Its evaluation order is inherited from the underlying Lisp: If the arguments to cons in list-of-values are evaluated from left to right, then list-of-values will evaluate operands from left to right; and if the arguments to cons are evaluated from right to left, then list-of-values will evaluate operands from right to left."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (list-of-values exps env)\n",
    "  (if (no-operands? exps)\n",
    "      '()\n",
    "      (cons (eval (first-operand exps) env)\n",
    "            (list-of-values \n",
    "             (rest-operands exps) \n",
    "             env))))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (eval-if exp env)\n",
    "  (if (true? (eval (if-predicate exp) env))\n",
    "      (eval (if-consequent exp) env)\n",
    "      (eval (if-alternative exp) env)))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (eval-sequence exps env)\n",
    "  (cond ((last-exp? exps) \n",
    "         (eval (first-exp exps) env))\n",
    "        (else \n",
    "         (eval (first-exp exps) env)\n",
    "         (eval-sequence (rest-exps exps) \n",
    "                        env))))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (eval-assignment exp env)\n",
    "  (set-variable-value! \n",
    "   (assignment-variable exp)\n",
    "   (eval (assignment-value exp) env)\n",
    "   env)\n",
    "  'ok)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (eval-definition exp env)\n",
    "  (define-variable! \n",
    "    (definition-variable exp)\n",
    "    (eval (definition-value exp) env)\n",
    "    env)\n",
    "  'ok)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Representing Expressions"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (self-evaluating? exp)\n",
    "  (cond ((number? exp) #t)\n",
    "        ((string? exp) #t)\n",
    "        (else #f)))\n",
    "\n",
    "(define (quoted? exp)\n",
    "  (tagged-list? exp 'quote))\n",
    "\n",
    "(define (text-of-quotation exp) (cadr exp))\n",
    "\n",
    "(define (tagged-list? exp tag)\n",
    "  (if (pair? exp)\n",
    "      (eq? (car exp) tag)\n",
    "      #f))\n",
    "\n",
    "(define (variable? exp) (symbol? exp))\n",
    "\n",
    "(define (assignment? exp)\n",
    "  (tagged-list? exp 'set!))\n",
    "\n",
    "(define (assignment-variable exp) (cadr exp))\n",
    "\n",
    "(define (assignment-value exp) (caddr exp))\n",
    "\n",
    "\n",
    "(define (definition? exp)\n",
    "  (tagged-list? exp 'define))\n",
    "\n",
    "(define (definition-variable exp)\n",
    "  (if (symbol? (cadr exp))\n",
    "      (cadr exp)\n",
    "      (caadr exp)))\n",
    "\n",
    "(define (definition-value exp)\n",
    "  (if (symbol? (cadr exp))\n",
    "      (caddr exp)\n",
    "      (make-lambda (cdadr exp)\n",
    "                   (cddr exp))))\n",
    "\n",
    "(define (lambda? exp) (tagged-list? exp 'lambda))\n",
    "\n",
    "(define (lambda-parameters exp) (cadr exp))\n",
    "(define (lambda-body exp) (cddr exp))\n",
    "\n",
    "(define (make-lambda parameters body)\n",
    "  (cons 'lambda (cons parameters body)))\n",
    "\n",
    "\n",
    "(define (if? exp) (tagged-list? exp 'if))\n",
    "\n",
    "(define (if-predicate exp) (cadr exp))\n",
    "\n",
    "(define (if-consequent exp) (caddr exp))\n",
    "\n",
    "(define (if-alternative exp)\n",
    "  (if (not (null? (cdddr exp)))\n",
    "      (cadddr exp)\n",
    "      'false))\n",
    "\n",
    "(define (make-if predicate consequent alternative)\n",
    "  (list 'if predicate consequent alternative))\n",
    "\n",
    "\n",
    "(define (begin? exp) (tagged-list? exp 'begin))\n",
    "\n",
    "(define (begin-actions exp) (cdr exp))\n",
    "\n",
    "(define (last-exp? seq) (null? (cdr seq)))\n",
    "(define (first-exp seq) (car seq))\n",
    "(define (rest-exps seq) (cdr seq))\n",
    "\n",
    "(define (sequence->exp seq)\n",
    "  (cond ((null? seq) seq)\n",
    "        ((last-exp? seq) (first-exp seq))\n",
    "        (else (make-begin seq))))\n",
    "\n",
    "(define (make-begin seq) (cons 'begin seq))\n",
    "\n",
    "\n",
    "(define (application? exp) (pair? exp))\n",
    "(define (operator exp) (car exp))\n",
    "(define (operands exp) (cdr exp))\n",
    "\n",
    "(define (no-operands? ops) (null? ops))\n",
    "(define (first-operand ops) (car ops))\n",
    "(define (rest-operands ops) (cdr ops))\n",
    "\n",
    "\n",
    "(define (cond? exp) (tagged-list? exp 'cond))\n",
    "\n",
    "(define (cond-clauses exp) (cdr exp))\n",
    "\n",
    "(define (cond-else-clause? clause)\n",
    "  (eq? (cond-predicate clause) 'else))\n",
    "\n",
    "(define (cond-predicate clause) (car clause))\n",
    "\n",
    "(define (cond-actions clause) (cdr clause))\n",
    "\n",
    "(define (cond->if exp)\n",
    "  (expand-clauses (cond-clauses exp)))\n",
    "\n",
    "(define (expand-clauses clauses)\n",
    "  (if (null? clauses)\n",
    "      'false                          ; no else clause\n",
    "      (let ((first (car clauses))\n",
    "            (rest (cdr clauses)))\n",
    "        (if (cond-else-clause? first)\n",
    "            (if (null? rest)\n",
    "                (sequence->exp (cond-actions first))\n",
    "                (error \"ELSE clause isn't last -- COND->IF\"\n",
    "                       clauses))\n",
    "            (make-if (cond-predicate first)\n",
    "                     (sequence->exp (cond-actions first))\n",
    "                     (expand-clauses rest))))))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (true? x)\n",
    "  (not (eq? x #f)))\n",
    "\n",
    "(define (false? x)\n",
    "  (eq? x #f))\n",
    "\n",
    "\n",
    "(define (make-procedure parameters body env)\n",
    "  (list 'procedure parameters body env))\n",
    "\n",
    "(define (compound-procedure? p)\n",
    "  (tagged-list? p 'procedure))\n",
    "\n",
    "\n",
    "(define (procedure-parameters p) (cadr p))\n",
    "(define (procedure-body p) (caddr p))\n",
    "(define (procedure-environment p) (cadddr p))\n",
    "\n",
    "\n",
    "(define (enclosing-environment env) (cdr env))\n",
    "\n",
    "(define (first-frame env) (car env))\n",
    "\n",
    "(define the-empty-environment '())\n",
    "\n",
    "(define (make-frame variables values)\n",
    "  (cons variables values))\n",
    "\n",
    "(define (frame-variables frame) (car frame))\n",
    "(define (frame-values frame) (cdr frame))\n",
    "\n",
    "(define (add-binding-to-frame! var val frame)\n",
    "  (set-car! frame (cons var (car frame)))\n",
    "  (set-cdr! frame (cons val (cdr frame))))\n",
    "\n",
    "(define (extend-environment vars vals base-env)\n",
    "  (if (= (length vars) (length vals))\n",
    "      (cons (make-frame vars vals) base-env)\n",
    "      (if (< (length vars) (length vals))\n",
    "          (error \"Too many arguments supplied\" vars vals)\n",
    "          (error \"Too few arguments supplied\" vars vals))))\n",
    "\n",
    "(define (lookup-variable-value var env)\n",
    "  (define (env-loop env)\n",
    "    (define (scan vars vals)\n",
    "      (cond ((null? vars)\n",
    "             (env-loop (enclosing-environment env)))\n",
    "            ((eq? var (car vars))\n",
    "             (car vals))\n",
    "            (else (scan (cdr vars) (cdr vals)))))\n",
    "    (if (eq? env the-empty-environment)\n",
    "        (error \"Unbound variable\" var)\n",
    "        (let ((frame (first-frame env)))\n",
    "          (scan (frame-variables frame)\n",
    "                (frame-values frame)))))\n",
    "  (env-loop env))\n",
    "\n",
    "(define (set-variable-value! var val env)\n",
    "  (define (env-loop env)\n",
    "    (define (scan vars vals)\n",
    "      (cond ((null? vars)\n",
    "             (env-loop (enclosing-environment env)))\n",
    "            ((eq? var (car vars))\n",
    "             (set-car! vals val))\n",
    "            (else (scan (cdr vars) (cdr vals)))))\n",
    "    (if (eq? env the-empty-environment)\n",
    "        (error \"Unbound variable -- SET!\" var)\n",
    "        (let ((frame (first-frame env)))\n",
    "          (scan (frame-variables frame)\n",
    "                (frame-values frame)))))\n",
    "  (env-loop env))\n",
    "\n",
    "(define (define-variable! var val env)\n",
    "  (let ((frame (first-frame env)))\n",
    "    (define (scan vars vals)\n",
    "      (cond ((null? vars)\n",
    "             (add-binding-to-frame! var val frame))\n",
    "            ((eq? var (car vars))\n",
    "             (set-car! vals val))\n",
    "            (else (scan (cdr vars) (cdr vals)))))\n",
    "    (scan (frame-variables frame)\n",
    "          (frame-values frame))))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "1"
      ]
     },
     "execution_count": 10,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "(eval 1 2)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 4.1.4 Running the Evaluator as a Program"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      ";;; M-Eval input:\n",
      "2\n",
      "\n",
      ";;; M-Eval value:\n",
      "2\n",
      "\n",
      ";;; M-Eval input:\n",
      "(+ 2 3)\n",
      "\n",
      ";;; M-Eval value:\n",
      "(+ 2 3)\n",
      "\n",
      ";;; M-Eval input:\n"
     ]
    }
   ],
   "source": [
    "\n",
    "(define (setup-environment)\n",
    "  (let ((initial-env\n",
    "         (extend-environment (primitive-procedure-names)\n",
    "                             (primitive-procedure-objects)\n",
    "                             the-empty-environment)))\n",
    "    (define-variable! 'true #t initial-env)\n",
    "    (define-variable! 'false #f initial-env)\n",
    "    initial-env))\n",
    "\n",
    ";[do later] (define the-global-environment (setup-environment))\n",
    "\n",
    "(define (primitive-procedure? proc)\n",
    "  (tagged-list? proc 'primitive))\n",
    "\n",
    "(define (primitive-implementation proc) (cadr proc))\n",
    "\n",
    "(define primitive-procedures\n",
    "  (list (list 'car car)\n",
    "        (list 'cdr cdr)\n",
    "        (list 'cons cons)\n",
    "        (list 'null? null?)\n",
    ";;      more primitives\n",
    "        ))\n",
    "\n",
    "(define (primitive-procedure-names)\n",
    "  (map car\n",
    "       primitive-procedures))\n",
    "\n",
    "(define (primitive-procedure-objects)\n",
    "  (map (lambda (proc) (list 'primitive (cadr proc)))\n",
    "       primitive-procedures))\n",
    "\n",
    ";[moved to start of file] (define apply-in-underlying-scheme apply)\n",
    "\n",
    "(define (apply-primitive-procedure proc args)\n",
    "  (apply-in-underlying-scheme\n",
    "   (primitive-implementation proc) args))\n",
    "\n",
    "\n",
    "\n",
    "(define input-prompt \";;; M-Eval input:\")\n",
    "(define output-prompt \";;; M-Eval value:\")\n",
    "\n",
    "(define (driver-loop)\n",
    "  (prompt-for-input input-prompt)\n",
    "  (let ((input (read)))\n",
    "    (let ((output (eval input the-global-environment)))\n",
    "      (announce-output output-prompt)\n",
    "      (user-print output)))\n",
    "  (driver-loop))\n",
    "\n",
    "(define (prompt-for-input string)\n",
    "  (newline) (newline) (display string) (newline))\n",
    "\n",
    "(define (announce-output string)\n",
    "  (newline) (display string) (newline))\n",
    "\n",
    "(define (user-print object)\n",
    "  (if (compound-procedure? object)\n",
    "      (display (list 'compound-procedure\n",
    "                     (procedure-parameters object)\n",
    "                     (procedure-body object)\n",
    "                     '<procedure-env>))\n",
    "      (display object)))\n",
    "\n",
    ";;;Following are commented out so as not to be evaluated when\n",
    ";;; the file is loaded.\n",
    "(define the-global-environment (setup-environment))\n",
    "(driver-loop)\n",
    "\n",
    "'METACIRCULAR-EVALUATOR-LOADED"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "(setup-environment)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Calysto Scheme 3",
   "language": "scheme",
   "name": "calysto_scheme"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "scheme"
   },
   "mimetype": "text/x-scheme",
   "name": "scheme",
   "pygments_lexer": "scheme"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
